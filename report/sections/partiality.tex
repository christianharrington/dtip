%!TEX root = ../main.tex
\section{Introducing General Recursion}
\label{sec:partiality}
% No longer has strong normalization
%TAPL

In this section, we investigate the implications of adding general recursion to the language. Because the simply typed lambda calculus has the property of strong normalization, we cannot define recursive functions directly as terms in the language\,\cite[p. 143]{Pierce:TypeSystems}. Instead, we can extend the language with a \texttt{Fix} primitive with appropriate evaluation rules, which acts as a generator for generally recursive functions. An example use of \texttt{Fix} is shown in Figure~\ref{fig:factorial}. We could also have chosen to implement the arguably more practical \texttt{letrec} construct suggested by Pierce\,\cite[p. 144]{Pierce:TypeSystems}. However, since we have not implemented let-expressions, these would first have to implemented for this to work. Furthermore, the added convenience provided by let-expressions (and thereby \texttt{letrec}) is lessened by the fact that we have not implemented named variables. Therefore, the amount of work required to implement \texttt{letrec} was deemed too high compared to the potential benefits.

\begin{figure}
\begin{alltt}
fact : Expr G (TipFun TipInt TipInt)
fact = Fix (Lam 
             (Lam 
                -- if x == 0
                (If (OpB Eql (Val 0) (Var stop))
                  (Val 1) -- then 1
                  -- else (x * f (x - 1))
                  (OpB Mul (Var stop) 
                           (App (Var (pop stop)) 
                                (OpB Sub (Var stop) (Val 1))))))) 
\end{alltt}
\caption{The factorial function defined in the well-typed representation. \texttt{Var stop} is the input, while \texttt{Var pop stop} refers to the function.}
\label{fig:factorial}
\end{figure}

In the definitions of the language presented so far, we have paid no special attention to the presence of \texttt{Fix}. Nevertheless, adding general recursion poses some new challenges when it comes to correctness of evaluation.

\subsection{The Problem with Interpreting Fixed Points}
It is tempting to think that we can safely follow the example in Figure~\ref{fig:interp-fix} and add a case for \texttt{Fix} to the well-typed interpreter, the \texttt{interp} function, from Section~\ref{sec:a-well-typed-expression-language}:

\begin{figure}
\begin{alltt}
  interp : Env G \(\rightarrow\) Expr G t \(\rightarrow\) interpTip t
  \vdots
  interp env (Fix f) = (interp env f) (interp env (Fix f))
\end{alltt}
\caption{Adding \texttt{Fix} to \texttt{interp}. Type correct, but not accepted by the totality checker.}
\label{fig:interp-fix}
\end{figure}

However, this will not do. Since \texttt{Fix} allows us to write infinitely recursive functions, the interpreter may loop forever. As a consequence, the Idris totality checker will complain that \texttt{interp} is no longer total. Totality means that for any total function, any input will always produce an output\,\cite{Turner04totalfunctional}. This property is important in consideration of the Curry-Howard correspondence, as we cannot regard a function to be a proof of its type unless all cases of the input has been covered. Furthermore, partial functions are not evaluated by the type checker, as they may cause type checking to loop indefinitely. Consequently, while we can now define more interesting functions using recursion, we no longer have the ability to reason about the result of interpretation within Idris. To remedy this situation, we must rethink our approach.

\subsection{Interpretation as a Relation}
%multistep
%extrinsic vs. intrinsic
Instead of interpreting our well-typed representation using a function, we will create an inductively defined relation \texttt{Step} which models a step of evaluation for a given term. This will amount to defining small-step semantics for the language. A similar approach to ours, implemented in Coq, can be found in \textit{Software Foundations} by Pierce et al.\,\cite{Pierce:SF}, but they do not fully take advantage of the power of dependent types.

\begin{figure}
\begin{alltt}
  data Step : Expr G t \(\rightarrow\) Expr G' t \(\rightarrow\) Type where
      -- Application
      S_App1    : \{t1: Expr G (TipFun t t')\} \(\to\) 
                  \{t1': Expr G (TipFun t t')\} \(\to\) 
                  \{t2: Expr G t\} \(\rightarrow\)
                  Step t1 t1' \(\rightarrow\)
                  Step (App t1 t2) (App t1' t2)
      S_App2    : \{t2: Expr G t, t2': Expr G t, f: Expr G (TipFun t t')\} \(\rightarrow\)
                  NormalForm G f \(\rightarrow\)
                  Step t2 t2' \(\rightarrow\)
                  Step (App f t2) (App f t2')
      S_AppBeta : \{v: Expr G t, b: Expr (t :: G) t', b': Expr G t'\} \(\rightarrow\)
                  NormalForm G v \(\rightarrow\)
                  Step (App (Lam b) v) (subst \{Glam=[]\} b stop v)
      -- Fix
      S_Fix     : \{f: Expr G (TipFun t t), f': Expr G' (TipFun t t)\} \(\rightarrow\)
                  Step f f' \(\rightarrow\)
                  Step (Fix f) (Fix f')
      S_FixBeta : \{b: Expr (t :: G) t, b': Expr G t\} \(\rightarrow\)
                  Step (Fix (Lam b)) (subst \{Glam=[]\} b stop (Fix (Lam b)))
      \vdots
\end{alltt}
\caption{The Step evaluation relation with trivial parts omitted.}
\label{fig:step-relation}
\end{figure}

\texttt{Step} is an evaluation relation which maps an input \texttt{Expr G t} to an output \texttt{Expr G' t}. The reason that the resulting environment \texttt{G'} is not specified more precisely is that it may or may not change, depending on whether or not substitution is performed. Each of the cases have been translated directly and straightforwardly from the corresponding small-step evaluation rules specified by Pierce\,\cite{Pierce:TypeSystems}.

In contrast to the intrinsic proof of correctness provided by a total \texttt{interp} function, defining the semantics for the language as a relation means that evaluation must now be given as an extrinsic proof. With \texttt{Step}, we can prove that a well-typed expression \textit{a} evaluates to a well-typed expression \textit{b} in one step. Due to the Curry-Howard correspondence, using a given constructor of \texttt{Step} corresponds to applying the corresponding evaluation rule. Any parameter to that rule will thus provide an additional proof obligation, which must be discharged using a \texttt{Step} (or \texttt{NormalForm}) constructor.

Evaluating a non-terminating expression is equivalent to giving an infinitely long and repetitive proof, which is obviously not possible. However, a promising alternative to providing evaluation as a relation is Nils Anders Danielsson's use of a partiality monad\,\cite{Danielsson:2012:OSU:2364527.2364546}. In this approach it is possible to reason about possibly infinite evaluation using coinduction. Specifically, it is possible to reason about which computations that must return a value \texttt{now}, and which that will return \texttt{later}. We have not explicitly explored this approach for the present work, but the partiality monad seems to be a quite powerful tool.

\begin{figure}
\begin{alltt}
  data NormalForm : (G: Vect n Tip) \(\rightarrow\) Expr G t \(\rightarrow\) Type where
    NF_Unit : NormalForm G U
    NF_Int  : NormalForm G (Val i)
    NF_Bool : NormalForm G (Boo b)
    NF_Lam  : \{b: Expr (t :: G) t'\} \(\rightarrow\) NormalForm G (Lam b)
    NF_Pair : \{a: Expr G t\} \(\rightarrow\) \{b: Expr G t'\} \(\rightarrow\) 
              NormalForm G a \(\rightarrow\) NormalForm G b \(\rightarrow\) NormalForm G (Pair a b)
    NF_InL  : {a: Expr G t} \(\rightarrow\) NormalForm G a \(\rightarrow\) (b: Tip) \(\rightarrow\) 
              NormalForm G (InL a b)
    NF_InR  : {b: Expr G t} \(\rightarrow\) NormalForm G b \(\rightarrow\) (a: Tip) \(\rightarrow\) 
              NormalForm G (InR b a)
\end{alltt}
\caption{The NormalForm relation.}
\label{fig:normal-forms}
\end{figure}
%Weak normal form (Sestoft) http://www.itu.dk/~sestoft/papers/sestoft-lamreduce.pdf
%evaluation only ever happens on closed terms
The \texttt{Step} relation depends on the definition of normal forms presented in Figure~\ref{fig:normal-forms}. Because we do not reduce under lambda abstractions, and at the same time reduce arguments before substitution, \texttt{NormalForm} is a representation of terms in \emph{weak normal form}\,\cite{Sestoft:reduction}. Since we have no evaluation rules for lambda abstractions and do not represent variables as normal forms (although a variable technically is in normal form), evaluation can only happen on closed terms.

\subsubsection{Shifting and Substitution}
Substitution is used for reduction of lambda terms. We can substitute a variable with an expression of the same type using $\beta$-reduction, yielding a term consisting only of values. When working with de Bruijn indices, substitution requires the operation of shifting, since remaining variables must be renumbered whenever one variable has been substituted.

In \textit{Types and Programming Languages}, Pierce defines a very concise and elegant way of doing shifting and substitution\,\cite[pp. 79-80]{Pierce:TypeSystems}. His definition of shifting, shown in Figure~\ref{fig:Pierce-shifting}, defines a \textit{d}-place shift of a term \textit{t} above cutoff \textit{c}. Figure~\ref{fig:Pierce-substitution} defines substitution of a term \textit{s} for a variable \textit{j} in a term \textit{t}.

\begin{figure}
\[\uparrow \begin{matrix} d \\ c \end{matrix}(k)\quad = \begin{cases} k \\ k+d \end{cases}\begin{matrix} if\quad k < c \\ if\quad k \ge c \end{matrix}\]
\[\uparrow \begin{matrix} d \\ c \end{matrix}(\lambda .{ t }_{ 1 }) = \lambda .{ \uparrow  }_{ c+1 }^{ d }({ t }_{ 1 })\]
\[\uparrow \begin{matrix} d \\ c \end{matrix}({ t }_{ 1 }{ t }_{ 2 }) = { \uparrow  }_{ c }^{ d }({ t }_{ 1 }) { \uparrow  }_{ c }^{ d }({ t }_{ 2 })\]
\caption{Definition 6.2.1: Shifting (from \textit{Types and Programming Languages})}
\label{fig:Pierce-shifting}
\end{figure}

\begin{figure}
\[[j\mapsto s]k\quad\quad=\begin{cases} s \\ k \end{cases}\begin{matrix} if\quad k=j\quad \\ otherwise \end{matrix}\]
\[[j\mapsto s](\lambda .{ t }_{ 1 })=\lambda .[j+1\mapsto { \uparrow  }_{ 0 }^{ 1 }(s)]{ t }_{ 1 }\]
\[[j\mapsto s]({ { t }_{ 1 }{ t }_{ 2 })=( }[j\mapsto s]{ t }_{ 1 }[j\mapsto s]{ t }_{ 2 })\]
\caption{Definition 6.2.4: Substitution (from \textit{Types and Programming Languages})}
\label{fig:Pierce-substitution}
\end{figure}

Using dependent types, we can give a very expressive definition of substitution, such as the one in Figure~\ref{fig:subst}, which is used for the \texttt{Step} relation. In contrast to Pierce's implementation, our substitution function has the added guarantees that substitution preserves typing, and that the variable on which we perform substitution is actually in the context. This is guaranteed by our richer representation of de Bruijn indices as proofs of membership in a typing context.  
\begin{figure}
\begin{alltt}
subst : \{Gglob : Vect (S n) Tip\} \(\rightarrow\) \{Glam: Vect m Tip\} \(\rightarrow\) 
                 (e: Expr (Glam ++ Gglob) t) \(\rightarrow\) 
                 HasType fZ Gglob t' \(\rightarrow\) (v: Expr (tail Gglob) t') \(\rightarrow\) 
                 Expr (Glam ++ (tail Gglob)) t
subst                     (App f x) i v = App (subst f i v) (subst x i v)
subst                       (Fix f) i v = Fix (subst f i v)
subst \{Glam\} \{t=TipFun x y\} (Lam b) i v = Lam $ subst \{Glam=x::Glam\} b i v
subst \{Glam\} \{Gglob\}        (Var y) i v = 
  case splitVar y of
    Left  (k ** y') => Var $ append y' (tail Gglob) -- Local var: 
                                                    -- Extend env of y'
                                                    -- with Gglob
    Right (k ** y') => weaken \{E=[]\} Glam (substGlobal y' i v) 
                                                    -- v or tail y'
\vdots
\end{alltt}
\caption{The substitution function used for the \texttt{Step} relation.}
\label{fig:subst}
\end{figure}
Our implementation of substitution has the following properties, which may not all be immediately obvious:
\begin{enumerate}
\item A variable is either defined in the local context, \texttt{Glam}, or the global context \texttt{Gglob}. Local variables are those which are added to the context when the function goes under a lambda abstraction (hence the name \texttt{Glam}).
\item Because our evaluation order is \emph{call-by-value}\,\cite[pp. 57-58]{Pierce:TypeSystems}, we always substitute on the first variable in the global context, \texttt{Gglob}, denoted by the membership predicate \texttt{HasType fZ Gglob t'} on which \texttt{subst} is parameterized. 
\item We must always return an expression defined in the context \texttt{Glam~++~(tail Gglob)}. That is, a context in which the first variable from the global context has been removed (as per property 2).
\item The cutoff from Definition 6.2.1 is given implicitly by the length of the local context, \texttt{Glam}. This means that all variables in \texttt{Glam} by definition are below the cutoff, while all variables in \texttt{Gglob} are equal to or greater than the cutoff.
%\item The expression which we substitute in, \texttt{v}, is defined in the context \texttt{tail Gglob}, due to property 2.
\end{enumerate}

In \texttt{subst}, the equivalent to [\textit{j}$\mapsto$\textit{s}]\textit{t} from Definition 6.2.4 is [\textit{i}$\mapsto$\textit{v}]\textit{e}, where \textit{i} is the parameter \texttt{HasType fZ Gglob t'}. In the following, we will use these specific variable names to keep the distinction clear. When met with a variable \textit{k} (called \texttt{y} in \texttt{subst}) and a variable to be substituted \textit{j}, Definition 6.2.4 asks the question: ``Is \textit{k} equal to \textit{j}?''. By a call to \texttt{splitVar} (Figure~\ref{fig:splitVar}), \texttt{subst} asks: ``Is \texttt{y} defined in the local or the global context?''

\begin{figure}
\begin{alltt}
splitVar : {G: Vect m Tip} \(\rightarrow\) \{G': Vect n Tip\} \(\rightarrow\) HasType i (G ++ G') t \(\rightarrow\)
           Either (k: Fin m ** HasType k G t) 
                  (k: Fin n ** HasType k G' t)
\end{alltt}
\caption{The type signature of \texttt{splitVar}. The implementation is not interesting.}
\label{fig:splitVar}
\end{figure}

\begin{itemize}
\item If \texttt{y} is local, we only need to append \texttt{tail Gglob} to the context of \texttt{y}, as per properties 2 and 3. This is equivalent to the \textit{otherwise} case in the first case of Definition 6.2.4. Due to property 4, \texttt{y} matches the $k < c$ case of the variable case in Definition 6.2.1, so no shifting needs to be performed.
\item If \texttt{y} is global, then it might be the first variable in the global context, which means that \texttt{v} must be substituted for \texttt{y} (see property 2). We determine this by a call to the \texttt{substGlobal} function (Figure~\ref{fig:substGlobal}).
\end{itemize}

\begin{figure}
\begin{alltt}
    substGlobal : \{Gglob : Vect (S n) Tip\} \(\rightarrow\) (y: HasType i Gglob t) \(\rightarrow\) 
                  HasType fZ Gglob t' \(\rightarrow\) (v: Expr (tail Gglob) t') \(\rightarrow\) 
                  Expr (tail Gglob) t
    substGlobal stop    stop v = v
    substGlobal (pop y) stop v = Var y
\end{alltt}
\caption{The \texttt{substGlobal} function.}
\label{fig:substGlobal}
\end{figure}

\texttt{substGlobal} performs two important tasks by recursion on a \texttt{HasType} membership proof: (1) If \texttt{y} is indeed the first variable in the global context, then Idris's type system can unify \texttt{t} and \texttt{t'}, the types of \texttt{y} and \texttt{v}, since \texttt{v} has the same type as the first variable in \texttt{Gglob} (see type of \texttt{subst} or \texttt{substGlobal}). Hence substitution must be performed, which is equivalent to returning \textit{s} in the first case of Definition 6.2.4. (2) If \texttt{y} is not the first variable, then \texttt{substGlobal} shifts it down (from \texttt{pop y} to \texttt{y}), to account for the substitution of the first variable. This has the same effect as explicitly adding a shift down on the evaluation rule for lambda abstractions, as suggested by Pierce\,\cite[p.~81]{Pierce:TypeSystems}.

The value returned by \texttt{substGlobal} has type \texttt{Expr (tail Gglob) t}, regardless of whether substitution was performed. In accordance with properties 3 and 4, this value must now be shifted \textit{d} places up, where \textit{d} denotes the length of the local context, to obtain an \texttt{Expr (Glam ++ (tail Gglob)) t}. In the call to \texttt{weaken} (Figure~\ref{fig:weaken}) in \texttt{subst}, \texttt{prepend} performs the shifting corresponding to the $k \ge c$ case of the variable case in Definition 6.2.1. \texttt{prepend} simply performs \texttt{pop} \textit{d} times on any variable in the input expression, resulting in a \textit{d}-place shift. \texttt{subst} calls \texttt{weaken} with \texttt{E} defined as initially empty and \texttt{F} as the local context.

\begin{figure}
\begin{alltt}
weaken : \{E: Vect m Tip, G: Vect n Tip\} \(\rightarrow\) (F: Vect o Tip) \(\rightarrow\) 
         Expr (E ++ G) t \(\rightarrow\) Expr (E ++ F ++ G) t
weaken \{E\} \{G\} F (Var x) = 
  case splitVar x of
    Left (k ** x') => Var $ append (append x' F) G
    Right (k ** x') => Var $ prepend x' (E++F)
weaken \{t=TipFun x y\} \{E=E\} F (Lam b) = Lam $ weaken \{E=x::E\} F b
weaken F (App f v) = App (weaken F f) (weaken F v)
\vdots
\end{alltt}
\caption{The \texttt{weaken} function. All other cases than \texttt{Var} have been omitted, as they are uninteresting.}
\label{fig:weaken}
\end{figure}

Since we do not shift variables up whenever we go under a lambda abstraction, but instead shift them up in the \texttt{Var} case if needed, the lambda abstraction and application cases from Definition 6.2.4 are as trivial as shown in \texttt{subst}.

% The first case of definition 6.2.4 is the \texttt{Var} case. Based on the idea behind the cutoff parameter for shifting, we handle the \texttt{Var} case by first observing that \texttt{e} must be defined in a local context of \textit{m} variables, \texttt{Glam}, and a global non-empty context, \texttt{Gglob}. The variable on which we perform substitution is always the first variable in \texttt{Gglob}. The local context is added to each time we go under a lambda abstraction (hence the name, Glam). Variables in \texttt{Glam} are by definition below the cutoff and variables in the global context \texttt{Gglob} are by definition equal to or above the cutoff.

% We determine whether a variable is in \texttt{Glam} or \texttt{Gglob} by using the function \texttt{splitVar} (see Figure~\ref{fig:splitVar}). This function performs the same splitting as the first case in definition 6.2.1, returning a strengthened variable only parameterized by either the local or the global context, respectively. Because we always substitute on the first variable in \texttt{Gglob}, a local variable defined in \texttt{Glam} 


%  We cannot do substitution in an empty global context, as we would have no variable to substitute. We always do substitution on the first variable in the global context, so \texttt{HasType fZ Gglob t'}, denoted by \textit{i}, is a membership predicate for the variable we wish to substitute. In this sense, \textit{i} can be regarded as a ``pointer''. \texttt{v} is the expression which will replace the first variable in the global context. Therefore, \texttt{v} can only be defined in the context \texttt{tail Gglob}, since the substituted variable must not occur free in \texttt{v}. Finally, we can return an \texttt{Expr (Glam ++ tail Gglob) t}, where the first variable in \texttt{Gglob} has been substituted, and thus does not appear in the context of the returned expression.

% As is apparent from Figure~\ref{fig:subst}, the most interesting case of \texttt{subst} is the \texttt{Var} case. \texttt{splitVar} is a function which identifies to which part of the context (either the local or the global part) a specific variable belongs. In shifting terms, this operation is similar to determining whether the variable is below or above the cutoff.

% If the variable in question, \textit{y}, is below the cutoff (i.e. is in \texttt{Glam}), we can simply append \texttt{tail Gglob} to the variable and be done. If \textit{y} is above the cutoff, and thus in \texttt{Gglob}, we must determine whether it is the variable that we want to perform substitution on. This is handled by the \texttt{substGlobal} function in Figure~\ref{fig:substGlobal}. In \texttt{substGlobal}, our ``pointer'' predicate \textit{i} from earlier becomes crucial: If \textit{y} points to the same variable as \textit{i}, the type system can unify their types, \texttt{t} and \texttt{t'}. This means that the types of \texttt{e} and \texttt{v} from the definition of \texttt{subst} are equal, and thus \texttt{v} can be substituted for the variable in a type-safe manner.


% If \textit{y} points to a different variable than \textit{i}, we shift it down by returning a \texttt{Var y} from a \texttt{pop y} predicate. So the result of \texttt{substGlobal} is either a shifted down variable or the expression \texttt{v}, both an \texttt{Expr (tail Gglob)}. What remains is to \texttt{weaken} this expression with \texttt{Glam}, such that the resulting expression has type \texttt{Expr (Glam ++ Gglob) t'}. \texttt{weaken} is shown in Figure~\ref{fig:weaken}.


\subsubsection{Multistepping}
% MULTISTEP
While the \texttt{Step} relation provides a way to make a term take one step of evaluation, it is seldom the case that only one step is needed to reach a final state. Inspired by the approach taken in \textit{Software Foundations}\,\cite{Pierce:SF}, we provide a relation \texttt{MultiStep} which a allows you to provide evaluation proofs for arbitrarily many steps. An implementation of \texttt{MultiStep} is given in Figure~\ref{fig:MultiStep}.

\begin{figure}
\begin{alltt}
using (G'': Vect o Tip)
  data MultiStep : Expr G t \(\to\) Expr G' t \(\to\) Type where
     M_Refl : \{a: Expr G t\} \(\to\) MultiStep a a
     M_Step : \{a: Expr G t, b: Expr G' t, c: Expr G'' t\} \(\to\) 
              Step a b \(\to\) MultiStep b c \(\to\) MultiStep a c
\end{alltt}
\caption{The \texttt{MultiStep} relation. The \texttt{using} notation simply allows us to use G in the second constructor.}
\label{fig:MultiStep}
\end{figure}

\subsection{Discussion}
% Edwin Brady's solution 

In the Idris tutorial\,\cite{Brady:IdrisTutorial}, Brady has a different solution to the problem of defining recursive functions. He utilizes Idris's internal reduction semantics for defining a lazily evaluated application function \texttt{app} as a wrapper for the \texttt{App} constructor. 

\begin{figure}
\begin{alltt}
app : |(f : Expr G (TipFun a t)) \(\to\) Expr G a \(\to\) Expr G t
app = \textbackslash{f}, a => App f a
\end{alltt}
\caption{Edwin Brady's solution for recursive functions. Annotating a type with in `|' in Idris means that it will be lazily evaluated, such that the corresponding argument is only evaluated when needed.}
\end{figure}

With \texttt{app}, he obtains the ability to define recursive functions without adding new primitives to the language, and without extending the interpreter. The interpreter is thus kept total, but any recursively defined \texttt{Expr} expression is now deemed partial by the totality checker. As such, non-termination is still a possible result of interpretation. Also, recursion now needs to happen by calling an Idris function within the \texttt{Expr} expression, such that the language is no longer self-contained.